REM ********************
REM PARALLAX SCROLLER
REM BY TIMO KLOSS
REM DEMO WITH PARALLAX SCROLLING AND MUSIC
REM ********************

REM CREATE DIFFERENT LAYERS
DISPLAY 3
LAYER OPEN 0,128,64,0
LAYER OPEN 1,128,64,1
LAYER OPEN 2,128,64,1
LAYER OPEN 3,72,10,1

REM THE TEXT FOR THE SCROLLER
REM YOU CAN PUT YOUR OWN HERE!
T$="WELCOME TO LOWRES CODER! PLAY HOMEGROWN RETRO GAMES AND PROGRAM YOUR OWN! HAVE FUN :)       "

REM INITIALIZE MUSIC
DIM SNGARR(15,2),SNGCHA(63,15,1),SNGPHR(63,15,1),SNGPLAYER(2,1)
GOSUB INITINSTRUMENTS
RESTORE DEMOSONG
GOSUB READSONG

REM DRAW BACKGROUND LAYER
LAYER 0
CLS 13
REM SOME STARS
COLOR 12
FOR I=1 TO 10
  PLOT RND*64,RND*34
NEXT I
REM LANDSCAPE SHAPE
COLOR 0
LINE 0,42 TO 32,34
LINE 32,34 TO 64,42
REM DUPLICATE IMAGE FOR ENDLESS SCROLLING
GET BLOCK 0,0,0 TO 63,63
PUT BLOCK 0,64,0
COLOR 12
PAINT 0,63

REM DRAW FIRST PARALLAX LAYER
LAYER 1
COLOR 11
FOR I=1 TO 10
  PLOT RND*64,RND*26
NEXT I
COLOR 5
LINE 0,36 TO 24,45
LINE 24,45 TO 48,48
LINE 48,48 TO 64,36
GET BLOCK 0,0,0 TO 63,63
PUT BLOCK 0,64,0
COLOR 15
PAINT 0,63

REM DRAW SECOND PARALLAX LAYER
LAYER 2
COLOR 1
FOR I=1 TO 4
  PLOT RND*64,RND*18
NEXT I
COLOR 5
LINE 0,58 TO 12,50
LINE 12,50 TO 24,60
LINE 24,60 TO 48,52
LINE 48,52 TO 64,58
GET BLOCK 0,0,0 TO 63,63
PUT BLOCK 0,64,0
COLOR 14
PAINT 0,63

REM PREPARE TEXT LAYER
LAYER 3
PALETTE 15,0
FONT 3
COLOR ,,15
COL=1

DIM OFFX(3)

REM MAIN LOOP

DO
  GOSUB UPDATESONG
  
  REM MOVE PARALLAX LAYERS
  FOR I=0 TO 2
    LAYER OFFSET I,OFFX(I),0
    OFFX(I)=OFFX(I)+2^I
    IF OFFX(I)>=64 THEN OFFX(I)=OFFX(I)-64
  NEXT I
  
  REM MOVE TEXT LAYER UP AND DOWN
  S=S+0.1
  LAYER DISPLAY 3,0,28+SIN(S)*20
  
  REM SCROLL TEXT TO LEFT
  SCROLL 0,0 TO 72,8,-1,0 CLEAR
  TX=TX+1
  IF TX=8 THEN
    REM DRAW NEXT LETTER
    IF TP=LEN(T$) THEN TP=0
    TP=TP+1
    COLOR COL
    COL=COL+1
    IF COL=15 THEN COL=1
    TEXT 64,1,MID$(T$,TP,1),0,2
    TX=0
  END IF
  
  REM CYCLE COLORS ON TEXT LAYER
  CC=CC+1
  FOR I=1 TO 14
    PALETTE I,(CC+I) MOD 64
  NEXT I
  
  WAIT 0.04
LOOP

REM MUSIC CODE FOLLOWS

INITINSTRUMENTS:
REM ** STANDARDS
DEF SOUND 0,0
DEF SOUND 1,1
DEF SOUND 2,2
DEF SOUND 3,3
REM ** DRUMS
DEF SOUND 8,2,0.5,0.1
DEF SOUND LINE 8,0.1,24,0
DEF SOUND 9,3,0,0.1
DEF SOUND LINE 9,0.1,24,0
DEF SOUND 10,3,0,0.05
DEF SOUND 11,3,0,0.1
DEF SOUND LINE 11,0.1,-24,0
RETURN

READSONG:
REM SNGTEMPO IS UNUSED BUT RESERVED
READ SNGTICKS,SNGTEMPO
FOR SNGR=0 TO 15
  FOR SNGV=0 TO 2
    READ SNGARR(SNGR,SNGV)
  NEXT SNGV
NEXT SNGR
DO
  READ SNGC
  IF SNGC=-1 THEN EXIT
  FOR SNGCR=0 TO 15
    FOR SNGI=0 TO 1
      READ SNGCHA(SNGC,SNGCR,SNGI)
    NEXT SNGI
  NEXT SNGCR
LOOP
DO
  READ SNGP
  IF SNGP=-1 THEN EXIT
  FOR SNGPR=0 TO 15
    FOR SNGI=0 TO 1
      READ SNGPHR(SNGP,SNGPR,SNGI)
    NEXT SNGI
  NEXT SNGPR
LOOP
FOR SNGV=0 TO 2
  SNGPLAYER(SNGV,0)=0
  SNGPLAYER(SNGV,1)=FALSE
NEXT SNGV
SNGROW=0
SOUND WAIT
GOSUB UPDATESONG
SOUND RESUME
RETURN

UPDATESONG:
IF SOUND LEN>16 THEN RETURN
IF SNGROW=16 THEN
  SNGROW=0
ELSE IF SNGARR(SNGROW,0)=255 AND SNGARR(SNGROW,1) AND SNGARR(SNGROW,2)=255 THEN
  SNGROW=0
END IF
FOR SNGV=0 TO 2
  SNGC=SNGARR(SNGROW,SNGV)
  IF SNGC=255 THEN
    SOUND SNGV,0,16*SNGTICKS
  ELSE
    SNGCR=SNGPLAYER(SNGV,0)
    SNGP=SNGCHA(SNGC,SNGCR,0)
    IF SNGP=255 THEN
      SOUND SNGV,0,16*SNGTICKS
    ELSE
      SNGT=SNGCHA(SNGC,SNGCR,1)
      SNGCURR=0
      FOR SNGPR=0 TO 16
        IF SNGPR<16 THEN SNGN=SNGPHR(SNGP,SNGPR,0) ELSE SNGN=0
        IF SNGN<255 AND SNGPR>SNGCURR THEN
          SNGN=SNGPHR(SNGP,SNGCURR,0)
          SNGD=(SNGPR-SNGCURR)*SNGTICKS
          IF SNGN=0 OR SNGN=255 THEN
            SOUND SNGV,0,SNGD
          ELSE
            SNGS=SNGPHR(SNGP,SNGCURR,1)
            SOUND SNGV,SNGN+SNGT,SNGD,SNGS
          END IF
          SNGCURR=SNGPR
        END IF
      NEXT SNGPR
    END IF
  END IF
NEXT SNGV
SNGRDONE=TRUE
FOR SNGV=0 TO 2
  SNGC=SNGARR(SNGROW,SNGV)
  IF SNGC<255 THEN
    SNGCR=SNGPLAYER(SNGV,0)+1
    IF SNGCR<16 THEN SNGP=SNGCHA(SNGC,SNGCR,0) ELSE SNGP=255
    IF SNGP=255 THEN
      SNGCR=0
      SNGPLAYER(SNGV,1)=TRUE
    END IF
    SNGPLAYER(SNGV,0)=SNGCR
    IF NOT SNGPLAYER(SNGV,1) THEN SNGRDONE=FALSE
  END IF
NEXT SNGV
IF SNGRDONE THEN
  SNGROW=SNGROW+1
  FOR SNGV=0 TO 2
    SNGPLAYER(SNGV,0)=0
    SNGPLAYER(SNGV,1)=FALSE
  NEXT SNGV
END IF
RETURN


REM SONG MADE WITH LOWFI COMPOSER 2

DEMOSONG:
DATA 3,0
DATA 0,10,255,0,10,20,0,10,20,0,11,255
DATA 255,255,255,255,255,255,255,255,255,255,255,255
DATA 255,255,255,255,255,255,255,255,255,255,255,255
DATA 255,255,255,255,255,255,255,255,255,255,255,255
DATA 0
DATA 0,0,255,0,255,0,255,0
DATA 255,0,255,0,255,0,255,0
DATA 255,0,255,0,255,0,255,0
DATA 255,0,255,0,255,0,255,0
DATA 10
DATA 10,0,10,0,11,0,12,0
DATA 255,0,255,0,255,0,255,0
DATA 255,0,255,0,255,0,255,0
DATA 255,0,255,0,255,0,255,0
DATA 11
DATA 13,0,13,0,14,0,15,0
DATA 255,0,255,0,255,0,255,0
DATA 255,0,255,0,255,0,255,0
DATA 255,0,255,0,255,0,255,0
DATA 20
DATA 20,0,21,0,22,0,23,0
DATA 255,0,255,0,255,0,255,0
DATA 255,0,255,0,255,0,255,0
DATA 255,0,255,0,255,0,255,0
DATA -1
DATA 0
DATA 25,10,25,10,37,9,255,255
DATA 25,10,255,255,37,9,255,255
DATA 25,10,25,10,37,9,255,255
DATA 25,10,255,255,37,9,255,255
DATA 10
DATA 25,2,0,255,37,2,255,255
DATA 0,255,36,2,255,255,35,2
DATA 22,2,0,255,34,2,255,255
DATA 0,255,39,2,255,255,0,255
DATA 11
DATA 20,2,0,255,32,2,255,255
DATA 0,255,34,2,255,255,32,2
DATA 17,2,0,255,29,2,255,255
DATA 0,255,36,2,255,255,0,255
DATA 12
DATA 25,2,0,255,37,2,255,255
DATA 0,255,36,2,255,255,35,2
DATA 22,2,0,255,34,2,255,255
DATA 0,255,255,255,255,255,255,255
DATA 13
DATA 27,2,0,255,39,2,255,255
DATA 0,255,42,2,255,255,41,2
DATA 22,2,0,255,34,2,255,255
DATA 0,255,41,2,255,255,0,255
DATA 14
DATA 25,2,0,255,37,2,255,255
DATA 0,255,39,2,255,255,37,2
DATA 20,2,0,255,32,2,255,255
DATA 0,255,37,2,255,255,0,255
DATA 15
DATA 22,2,0,255,34,2,255,255
DATA 0,255,255,255,22,2,0,255
DATA 34,2,0,255,34,2,0,255
DATA 34,2,0,255,255,255,255,255
DATA 20
DATA 58,1,0,255,65,1,255,255
DATA 0,255,255,255,70,1,0,255
DATA 73,1,0,255,72,1,255,255
DATA 68,1,0,255,70,1,255,255
DATA 21
DATA 68,1,0,255,65,1,255,255
DATA 0,255,255,255,58,1,0,255
DATA 61,1,0,255,60,1,255,255
DATA 56,1,0,255,53,1,255,255
DATA 22
DATA 56,1,0,255,60,1,255,255
DATA 0,255,255,255,61,1,255,255
DATA 56,1,0,255,53,1,255,255
DATA 49,1,0,255,44,1,255,255
DATA 23
DATA 46,1,255,255,255,255,255,255
DATA 0,255,255,255,255,255,255,255
DATA 58,1,57,1,56,1,55,1
DATA 54,1,53,1,52,1,51,1
DATA -1