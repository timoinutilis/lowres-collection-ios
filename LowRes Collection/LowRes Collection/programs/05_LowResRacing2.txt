REM ********************
REM LOWRES RACING 2
REM BY TIMO KLOSS
REM RACING GAME
REM COLLECT COINS AND AVOID CAR CRASHES!
REM USE LEFT/RIGHT FOR CONTROL
REM OR ALTERNATIVELY A/B BUTTONS FOR RIGHT
REM ********************

RANDOMIZE TIMER

PERSIST HISCORE
DIM NT(2,2),ARR(15)

RESTORE SPRITES
FOR N=0 TO 6
  FOR I=0 TO 15
    READ ARR(I)
  NEXT I
  READ C0,C1,C2,C3
  DEF SPRITE N,ARR,C1,C2,C3
NEXT N

REM MUSIC INSTRUMENTS
DEF SOUND 1,2,0.5
DEF SOUND 2,0
DEF SOUND 3,3,0,0.1
DEF SOUND 4,3,0,0.08

REM SOUND FX
DEF SOUND 10,2,0.5
DEF SOUND 11,2,0.3
DEF SOUND 12,2,0.1
DEF SOUND 13,1
DEF SOUND 14,3
DEF SOUND LINE 14,0.5,24,0

GAMEINTRO:

GAMEPAD 0
SPRITE OFF

REM DRAWS TITLE BACKGROUND
COLOR 9
BAR 0,31 TO 63,63
COLOR 10
FOR I=1 TO 20
  PLOT RND*64,RND*31+31
NEXT I
COLOR 3
FOR Y=0 TO 32
  BAR 30-Y,31+Y TO 33+Y,31+Y
NEXT Y
FONT 3
COLOR 1,,0
TEXT 31,32,"II",1,1

SPRITE 0,4,26,5
SPRITE 1,53,26,6

RESTORE MUSIC
GOSUB PLAYSONG

FONT 0
COLOR 2
TEXT 32,51,"HI:"+HISCORE,1

C=8
REPEAT
  COLOR 11
  BAR 0,0 TO 63,30

  COLOR 1
  FONT 2
  TEXT 31,8,"LOWRES",1,1
  TEXT 31,16,"RACING",1,1

  REM RENDERS WAVE EFFECT ON TEXT
  SR=SR-0.2
  FOR SX=0 TO 63
    SCROLL SX,0 TO SX,30,0,SIN(SR+SX/64*PI)*8
  NEXT SX

  C=C-1
  IF C=5 THEN C=8
  COLOR C
  FONT 0
  TEXT 31,57,"PRESS BUTTON",1

  WAIT 0.04
UNTIL BUTTON TAP(0)

SOUND OFF
SPRITE OFF

START:

REM INITIALIZES GAME VARIABLES
CARX=28
CARY=52
BLOCKY=0
SCORE=0
BLOCKISBAD=FALSE
LEVEL=1
LIVES=3

CLS
FOR I=1 TO 31
  GOSUB SCROLLBG
NEXT I
GOSUB HUD

REM TREES
SPRITE 4,2,0,5
SPRITE 5,54,32,6

GAMEPAD 1

DO
  GOSUB SCROLLBG
  
  REM MOVE THE CAR
  IF LEFT(0) AND CARX>16 THEN CARX=CARX-2
  IF (RIGHT(0) OR BUTTON(0)) AND CARX<40 THEN CARX=CARX+2
  SPRITE 1,CARX,CARY,0

  REM RESET THE BLOCK WHEN REACHED BOTTOM
  IF BLOCKY>62 THEN BLOCKY=0
  IF BLOCKY=0 THEN
    BLOCKX=16+RND*25
    BLOCKISBAD=RND<0.3
  END IF
  
  REM MOVE BLOCK
  IF BLOCKISBAD THEN BIMG=1 ELSE BIMG=2
  BLOCKY=BLOCKY+4
  SPRITE 2,BLOCKX,BLOCKY-6,BIMG
  
  REM MOVE TREES
  FOR I=4 TO 5
    Y=SPRITE Y(I)
    Y=Y+2
    IF Y>63 THEN
      SPRITE I,,-8-RND*8,INT(5+RND*2)
    ELSE
      SPRITE I,,Y
    END IF
  NEXT I

  REM CHECK BLOCK COLLISION WITH CAR
  IF SPRITE HIT(1,2) THEN
    IF NOT BLOCKISBAD THEN
      SPRITE 2,,,3
      SCORE=SCORE+1
      GOSUB HUD
      SOUND 1,60,2,13
      SOUND 1,67,2
      SOUND 1,72,2
    ELSE
      SPRITE 0,CARX,CARY-5,4
      SOUND 1,20,6,14
      SOUND 0,1,0,10
      LIVES=LIVES-1
      WAIT 0.5
      GOSUB HUD
      IF LIVES=0 THEN EXIT ELSE SPRITE OFF 0
    END IF
    BLOCKY=0
    IF SCORE MOD 5=0 THEN LEVEL=LEVEL+1
  END IF
  
  SI=(SI+1) MOD 3
  SOUND 0,MIN(20+LEVEL,96),0,10+SI

  REM SPEED DEPENDS ON LEVEL
  DELAY=0.08-(LEVEL/100)
  IF DELAY<0.04 THEN DELAY=0.04
  WAIT DELAY
LOOP

REM GAME OVER!
GAMEPAD 0
IF SCORE>HISCORE THEN
  HISCORE=SCORE
  ENDTXT$="NEW HIGHSCORE"
  SUBMITSCORE SCORE
ELSE
  ENDTXT$="GAME OVER"
END IF
SOUND 0,1,0,10
SOUND 1,14,6
C=8
LAYER 1
REPEAT
  COLOR C
  C=C-1
  IF C=5 THEN C=8
  TEXT 31,20,ENDTXT$,1
  TEXT 31,40,"PRESS BUTTON",1
  WAIT 0.1
UNTIL BUTTON TAP(0)
CLS
LAYER 0
GOTO GAMEINTRO


SCROLLBG:

SCROLL 0,0 TO 63,63,0,2

REM DRAW THE GREEN SIDES
COLOR 9
BAR 0,0 TO 15,1
BAR 47,0 TO 63,1
COLOR 10
IF RND<0.2 THEN PLOT RND*16,0
IF RND<0.2 THEN PLOT 47+RND*17,0

REM DRAW THE STREET
COLOR 3
BAR 16,0 TO 46,1

REM DRAW THE SIDE LINES
IF STREETC<2 THEN COLOR 4 ELSE COLOR 1
STREETC=STREETC+1
IF STREETC=4 THEN STREETC=0
BAR 15,0 TO 15,1
BAR 47,0 TO 47,1
IF STREETC<2 THEN
  COLOR 2
  BAR 25,0 TO 25,1
  BAR 37,0 TO 37,1
END IF

RETURN


HUD:
LAYER 1
CLS
COLOR 1
TEXT 31,1,SCORE,1
COLOR 13
FOR I=0 TO LIVES-2
BAR 1+I*3,1 TO I*3+2,3
NEXT I
LAYER 0
RETURN


PLAYSONG:
MTEMPO=4
IOFF=0
FOR I=0 TO 33
  FOR C=0 TO 2
    READ PTCH,INST
    IF PTCH>-1 THEN
      IF NT(C,2)>0 THEN
        SOUND C,NT(C,0),NT(C,2)*MTEMPO,NT(C,1)
      END IF
      NT(C,0)=PTCH
      NT(C,1)=INST+IOFF
      NT(C,2)=1
    ELSE
      NT(C,2)=NT(C,2)+1
    END IF
  NEXT C
NEXT I
FOR C=0 TO 2
  IF NT(C,2)>0 THEN
    SOUND C,NT(C,0),NT(C,2)*MTEMPO,NT(C,1)
  END IF
  NT(C,0)=0
  NT(C,1)=0
  NT(C,2)=0
NEXT C
RETURN


MUSIC:
DATA 58,1,27,2,25,4
DATA -1,0,39,2,-1,0
DATA -1,0,27,2,73,3
DATA -1,0,39,2,-1,0
DATA 54,1,27,2,25,4
DATA 51,1,39,2,-1,0
DATA -1,0,27,2,73,3
DATA 0,0,39,2,25,4
DATA 58,1,27,2,25,4
DATA -1,0,39,2,-1,0
DATA 0,0,27,2,73,3
DATA 49,1,39,2,-1,0
DATA -1,0,27,2,25,4
DATA 0,0,39,2,-1,0
DATA 51,1,27,2,73,3
DATA -1,0,39,2,-1,0
DATA 58,1,30,2,25,4
DATA -1,0,42,2,-1,0
DATA -1,0,30,2,73,3
DATA -1,0,42,2,-1,0
DATA 54,1,30,2,25,4
DATA 58,1,42,2,-1,0
DATA -1,0,30,2,73,3
DATA 0,0,42,2,25,4
DATA 56,1,32,2,25,4
DATA -1,0,44,2,-1,0
DATA 0,0,32,2,73,3
DATA 54,1,44,2,-1,0
DATA -1,0,32,2,25,4
DATA 0,0,44,2,-1,0
DATA 56,1,32,2,73,3
DATA -1,0,44,2,25,3
DATA 51,1,27,2,25,4
DATA -1,0,-1,0,-1,0
DATA 0,0,0,0,-1,0


SPRITES:
DATA 42,160,234,172,229,108,37,96,42,160,234,172,229,108,42,160
DATA 0,11,13,0
DATA 42,160,229,108,234,172,42,160,37,96,229,108,234,172,42,160
DATA 0,1,4,0
DATA 37,128,150,96,90,80,86,80,150,96,37,128,0,0,0,0
DATA 0,8,7,3
DATA 66,4,32,32,0,0,128,8,0,0,32,32,66,4,0,0
DATA 0,8,7,3
DATA 128,0,10,130,9,96,37,88,37,88,40,160,128,0,0,32
DATA 0,1,2,4
DATA 5,0,31,64,117,80,87,80,117,80,21,64,10,0,10,0
DATA 0,10,5,9
DATA 1,0,5,64,7,64,21,80,29,80,85,84,21,80,2,0
DATA 0,10,5,9
